class Game {
    init() {
        this.board = Board();
        this.selected = nil; // [piece, moves]
    }

    clickTile(x, y) {
        var clickedPiece = this.board.get(x, y);

        if (this.selected == nil) {
            // Nothing selected yet
            if (clickedPiece != nil and clickedPiece.color == this.board.turn) {
                var moves = clickedPiece.getLegalMoves();
                this.selected = [clickedPiece, moves];
            }
        } else {
            var piece = this.selected[0];
            var moves = this.selected[1];

            var legal = false;
            var i = 0;
            while (i < moves.length()) {
                var move = moves[i];
                if (move[0] == x and move[1] == y) {
                    legal = true;
                    break;
                }
                i = i + 1;
            }

            if (legal) {
                if (piece.type == "king" and this.abs(x - piece.pos[0]) == 2) {
                    var rookFromCol;
                    var rookToCol;
                    if (x > piece.pos[0]) {
                        rookFromCol = 7;
                        rookToCol = x - 1;
                    } else {
                        rookFromCol = 0;
                        rookToCol = x + 1;
                    }
                    this.board.movePiece([rookFromCol, y], [rookToCol, y]);
                    if (this.board.turn == "white") {
                        this.board.turn = "black";
                    } else {
                        this.board.turn = "white";
                    }
                }

                this.board.movePiece(piece.pos, [x, y]);

                if(piece.type == "pawn"){
                    if(piece.color == "white" and y == 0){
                        this.board.set([x, y], Queen([x, y], "white", this.board));
                    }
                    if(piece.color == "black" and y == 7){
                        this.board.set([x, y], Queen([x, y], "black", this.board));
                    }
                }
                if(this.board.wKing.isInCheck() and this.board.wKing.isInMate())
                    println("Black Won!");
                if(this.board.bKing.isInCheck() and this.board.bKing.isInMate())
                    println("White Won!");
            }

            this.selected = nil; // Deselect after click (move or cancel)
        }
    }

    abs(x){
        if(x < 0) return -x;
        return x;
    }
}
